plugins {
    id "jacoco"
    id "maven-publish"

    id "io.micronaut.library" version "2.0.3"
    id "org.sonarqube" version "3.3"
    id "com.diffplug.spotless" version "5.11.0"
}

repositories {
    mavenLocal()
    mavenCentral()
}

group = groupId
version = artifactVersion

sourceCompatibility = 11
targetCompatibility = 11

spotless {
    java {
        encoding("UTF-8")
        importOrder()
        removeUnusedImports()
        eclipse().configFile("${projectDir}/config/codestyle.xml")
    }
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "goodforgod"
        property "sonar.projectKey", "GoodforGod_micronaut-arangodb"
    }
}

micronaut {
    version("3.0.0")
    processing {
        incremental(true)
        module(project.name)
        group(project.group)
        annotations("io.micronaut.configuration.arango.*")
    }
}

dependencies {
    annotationProcessor "io.micronaut:micronaut-inject-java"
    compileOnly "io.micronaut:micronaut-inject-java"

    api "com.arangodb:arangodb-java-driver:6.13.0"

    implementation "org.apache.httpcomponents:httpclient:4.5.13"
    implementation "io.micronaut:micronaut-management"
    implementation "io.projectreactor:reactor-core"

    testAnnotationProcessor "io.micronaut:micronaut-inject-java"

    testRuntimeOnly "ch.qos.logback:logback-classic:1.2.3"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"

    testImplementation "org.testcontainers:junit-jupiter:1.15.3"
    testImplementation "com.github.goodforgod:arangodb-testcontainer:1.3.0"

    testImplementation "io.micronaut.test:micronaut-test-core"
    testImplementation "io.micronaut.test:micronaut-test-junit5"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
}

test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
        exceptionFormat("full")
    }

    reports {
        html.enabled(false)
        junitXml.enabled(false)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.incremental = true
    options.fork = true
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = "Micronaut ArangoDB Configuration"
                url = "https://github.com/GoodforGod/micronaut-arangodb"
                description = "This is integration between Micronaut and ArangoDB."

                license {
                    name = "Apache License 2.0"
                    url = "https://github.com/GoodforGod/micronaut-arangodb/blob/master/LICENSE"
                    distribution = "repo"
                }

                developer {
                    id = "GoodforGod"
                    name = "Anton Kurako"
                    email = "goodforgod.dev@gmail.com"
                    url = "https://github.com/GoodforGod"
                }

                scm {
                    connection = "scm:git:git://github.com/GoodforGod/micronaut-arangodb.git"
                    developerConnection = "scm:git:ssh://GoodforGod/micronaut-arangodb.git"
                    url = "https://github.com/GoodforGod/micronaut-arangodb/tree/master"
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username System.getenv("OSS_USERNAME")
                password System.getenv("OSS_PASSWORD")
            }
        }
    }
}

check.dependsOn jacocoTestReport
jacocoTestReport {
    reports {
        xml.enabled true
        html.destination file("${buildDir}/jacocoHtml")
    }
}

if (project.hasProperty("signing.keyId")) {
    apply plugin: "signing"
    signing {
        sign publishing.publications.mavenJava
    }
}

javadoc {
    options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}