plugins {
    id "jacoco"
    id "maven-publish"

    id "org.sonarqube" version "4.3.0.3225"
    id "com.diffplug.spotless" version "6.19.0"
    id "io.micronaut.library" version "4.0.2"
    id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
}

repositories {
    mavenLocal()
    mavenCentral()
}

group = groupId
version = artifactVersion

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

micronaut {
    version("4.0.2")
    processing {
        incremental(true)
        module(project.name)
        group(project.group)
    }
}

configurations.configureEach { config ->
    if (config.name.startsWith("test")) {
        config.resolutionStrategy.dependencySubstitution {
            substitute(module("io.micronaut:micronaut-jackson-databind")).using(module("io.micronaut.serde:micronaut-serde-jackson:2.2.0"))
        }
    }
}

dependencies {
    annotationProcessor "io.goodforgod:graalvm-hint-processor:1.0.0"
    compileOnly "io.goodforgod:graalvm-hint-annotations:1.0.0"

    annotationProcessor "io.micronaut:micronaut-inject-java"
    compileOnly "io.micronaut:micronaut-inject-java"
    compileOnly "io.micronaut.serde:micronaut-serde-jackson"

    api "com.arangodb:arangodb-java-driver:7.1.0"

    implementation "io.micronaut:micronaut-management"
    implementation "io.projectreactor:reactor-core"

    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testAnnotationProcessor "io.micronaut.serde:micronaut-serde-processor"

    testRuntimeOnly "com.arangodb:jackson-dataformat-velocypack:4.1.0"
    testRuntimeOnly "ch.qos.logback:logback-classic:1.4.9"
    testImplementation "org.testcontainers:junit-jupiter:1.17.6"
    testImplementation "com.github.goodforgod:arangodb-testcontainer:3.0.1"

    testCompileOnly "io.micronaut.serde:micronaut-serde-jackson"
    testImplementation "io.micronaut.test:micronaut-test-core"
    testImplementation "io.micronaut.test:micronaut-test-junit5"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
    testImplementation "org.junit.platform:junit-platform-launcher"
}

test {
    failFast(false)
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
        exceptionFormat("full")
        showStandardStreams(false)
    }

    reports {
        html.required = false
        junitXml.required = false
    }
}

spotless {
    java {
        encoding("UTF-8")
        importOrder()
        removeUnusedImports()
        eclipse("4.21").configFile("${rootDir}/config/codestyle.xml")
    }
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "goodforgod"
        property "sonar.projectKey", "GoodforGod_$artifactId"
    }
}

nexusPublishing {
    packageGroup = groupId
    repositories {
        sonatype {
            username = System.getenv("OSS_USERNAME")
            password = System.getenv("OSS_PASSWORD")
            nexusUrl.set(uri("https://oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = "Micronaut ArangoDB Configuration"
                url = "https://github.com/GoodforGod/$artifactId"
                description = "This is integration between Micronaut and ArangoDB."

                license {
                    name = "Apache License 2.0"
                    url = "https://github.com/GoodforGod/$artifactId/blob/master/LICENSE"
                    distribution = "repo"
                }

                developer {
                    id = "GoodforGod"
                    name = "Anton Kurako"
                    email = "goodforgod.dev@gmail.com"
                    url = "https://github.com/GoodforGod"
                }

                scm {
                    connection = "scm:git:git://github.com/GoodforGod/${artifactId}.git"
                    developerConnection = "scm:git:ssh://GoodforGod/${artifactId}.git"
                    url = "https://github.com/GoodforGod/$artifactId/tree/master"
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username System.getenv("OSS_USERNAME")
                password System.getenv("OSS_PASSWORD")
            }
        }
        if (!version.endsWith("SNAPSHOT")) {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/GoodforGod/$artifactId"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile) {
    options.encoding("UTF-8")
    options.incremental(true)
    options.fork = true
}

check.dependsOn jacocoTestReport
jacocoTestReport {
    reports {
        xml.required = true
        html.outputLocation = layout.buildDirectory.dir("jacocoHtml")
    }
}

javadoc {
    options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

if (project.hasProperty("signingKey")) {
    apply plugin: "signing"
    signing {
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenJava
    }
}