plugins {
    id 'jacoco'
    id 'java-library'
    id 'maven-publish'

    id 'org.sonarqube' version '3.0'
    id 'com.diffplug.gradle.spotless' version '4.4.0'
}

group = groupId
version = artifactVersion

sourceCompatibility = 1.8
targetCompatibility = 1.8

spotless {
    java {
        encoding 'UTF-8'
        removeUnusedImports()
        eclipse().configFile "${projectDir}/config/codestyle.xml"
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'goodforgod'
        property 'sonar.projectKey', 'GoodforGod_micronaut-arangodb'
    }
}

configurations {
    all {
        resolutionStrategy {
            cacheChangingModulesFor(8000, 'seconds') // check for updates every build
        }
    }
    micronautBom
    compileOnly.extendsFrom(micronautBom)
    implementation.extendsFrom(micronautBom)
    annotationProcessor.extendsFrom(micronautBom)
    testAnnotationProcessor.extendsFrom(micronautBom)
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    micronautBom platform('io.micronaut:micronaut-bom:1.3.6')

    annotationProcessor 'io.micronaut:micronaut-inject-java'
    compileOnly 'io.micronaut:micronaut-inject-java'

    api 'com.arangodb:arangodb-java-driver:6.6.3'

    implementation 'io.micronaut:micronaut-management'

    testAnnotationProcessor 'io.micronaut:micronaut-inject-java'
    testImplementation 'com.github.goodforgod:arangodb-testcontainer:1.2.0'
    testImplementation 'org.testcontainers:junit-jupiter:1.14.3'

    testRuntimeOnly 'ch.qos.logback:logback-classic:1.2.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    testImplementation 'io.micronaut.test:micronaut-test-core'
    testImplementation 'io.micronaut.test:micronaut-test-junit5'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Micronaut ArangoDB Configuration'
                url = 'https://github.com/GoodforGod/micronaut-arangodb'
                description = 'This is integration between Micronaut and ArangoDB.'

                license {
                    name = 'Apache License 2.0'
                    url = 'https://github.com/GoodforGod/micronaut-arangodb/blob/master/LICENSE'
                    distribution = 'repo'
                }

                developer {
                    id = 'GoodforGod'
                    name = 'Anton Kurako'
                    email = 'goodforgod.dev@gmail.com'
                    url = 'https://github.com/GoodforGod'
                }

                scm {
                    connection = 'scm:git:git://github.com/GoodforGod/micronaut-arangodb.git'
                    developerConnection = 'scm:git:ssh://GoodforGod/micronaut-arangodb.git'
                    url = 'https://github.com/GoodforGod/micronaut-arangodb/tree/master'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username System.getenv("OSS_USERNAME")
                password System.getenv("OSS_PASSWORD")
            }
        }
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

check.dependsOn jacocoTestReport
jacocoTestReport {
    reports {
        xml.enabled true
        html.destination file("${buildDir}/jacocoHtml")
    }
}

if (project.hasProperty("signing.keyId")) {
    apply plugin: 'signing'
    signing {
        sign publishing.publications.mavenJava
    }
}